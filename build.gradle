plugins {
    id 'java-library'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'signing'
}

defaultTasks 'check'

group = 'com.athaydes'
version = "2.5.1-groovy-4.0"
description = 'This project is a global extension for Spock to create test (or, in Spock terms, Specifications) reports.'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def groovyVersion = '4.0.13'
def spockVersion = '2.3-groovy-4.0'

repositories {
    mavenCentral()
}

dependencies {
    api "org.apache.groovy:groovy:${groovyVersion}"
    implementation "org.apache.groovy:groovy-xml:${groovyVersion}"
    implementation "org.apache.groovy:groovy-json:${groovyVersion}"
    implementation "org.apache.groovy:groovy-templates:${groovyVersion}"
    implementation platform( "org.spockframework:spock-bom:$spockVersion" )
    api "org.spockframework:spock-core", {
        exclude group: 'org.codehaus.groovy'
    }
    implementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation platform( 'org.junit:junit-bom:5.9.1' )
    testImplementation 'org.junit.platform:junit-platform-testkit'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "cglib:cglib-nodep:3.3.0"
    testImplementation "org.slf4j:slf4j-simple:1.7.36"
}

test {
    useJUnitPlatform()

    maxParallelForks = 4
    exclude '**/*FakeTest.class', '**/UnrolledSpec.class', '**/SpecIncludingExtraInfo.class'
    systemProperty 'project.buildDir', project.buildDir
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'debug'
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle( "Running test [{}]: {}", Thread.currentThread().id, descriptor )
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStackTraces = true
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Athaydes-Spock-Reports",
                "Implementation-Version": project.version,
                "Automatic-Module-Name": 'com.athaydes.spock.reports' )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if ( JavaVersion.current().isJava9Compatible() ) {
        options.addBooleanOption( 'html5', true )
    }
}

def getProjectProperty = { String propertyName ->
    project.properties[ propertyName ]
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            artifactId = 'spock-reports'
            from components.java
            versionMapping {
                usage( 'java-api' ) {
                    fromResolutionOf( 'runtimeClasspath' )
                }
                usage( 'java-runtime' ) {
                    fromResolutionResult()
                }
            }
            pom {
                inceptionYear = '2013'
                name = project.name
                packaging = 'jar'
                description = project.description

                url = 'https://github.com/renatoathaydes/spock-reports'

                scm {
                    connection = 'git@github.com:renatoathaydes/spock-reports.git'
                    developerConnection = 'git@github.com:renatoathaydes/spock-reports.git'
                    url = 'https://github.com/renatoathaydes/spock-reports'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'renatoathaydes'
                        name = 'Renato Athaydes'
                        email = 'renato@athaydes.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username getProjectProperty( 'ossrhUsername' )?.toString()
                password getProjectProperty( 'ossrhPassword' )?.toString()
            }
        }
    }
}

signing {
    if ( project.hasProperty( 'sign' ) ) {
        sign publishing.publications.mavenJava
    }
}
