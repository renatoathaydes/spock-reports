buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
        classpath "com.github.townsfolk:gradle-release:1.2"
    }
}
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

defaultTasks 'clean', 'jar', 'test'

group = 'com.athaydes'
version = "1.2.6-SNAPSHOT"

def groovyVersion = '2.0.8'
def spockVersion = '1.0-groovy-2.0'

repositories {
	mavenCentral()
}

//////// hack to add provided dependency support ////////
configurations {
    provided
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
    }
}
////////////////////////////////////////////////////////////


dependencies {
    provided "org.codehaus.groovy:groovy-all:${groovyVersion}"
    provided "org.spockframework:spock-core:${spockVersion}"

	testCompile "cglib:cglib-nodep:2.2.2"
}

test {
	exclude '**/FakeTest.class'
	systemProperty 'project.buildDir', project.buildDir
	testLogging.showStandardStreams = true
	beforeTest { descriptor ->
		logger.lifecycle( "Running test: " + descriptor )
	}
}

jar {
	manifest {
		attributes(
				"Implementation-Title": "Athaydes-Spock-Reports",
				"Implementation-Version": version )
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'bintray'
apply plugin: 'release'

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    publications = ["mavenJava"]
    pkg {
        repo = 'maven'
        name = 'spock-reports'
        licenses = ['Apache-2.0']
    }
}
bintrayUpload.dependsOn build, sourcesJar
createReleaseTag.dependsOn bintrayUpload

